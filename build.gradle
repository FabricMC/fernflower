apply plugin: 'java'
apply plugin: 'maven-publish'

compileJava {
  sourceCompatibility '1.8'
  targetCompatibility '1.8'
}

group = 'org.jetbrains'
archivesBaseName = 'intellij-fernflower'

version = '1.0.0'

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
  version = version + "." + "${ENV.BUILD_NUMBER}"
} else {
  version = version + '-SNAPSHOT'
}

sourceSets {
  main.java.srcDirs 'src'
  test.java.srcDirs 'test'
}

repositories { jcenter() }
dependencies {
  testCompile 'junit:junit:4.+'
  testCompile 'org.assertj:assertj-core:3.+'
}

jar {
  manifest {
    attributes 'Main-Class': 'org.jetbrains.java.decompiler.main.decompiler.ConsoleDecompiler'
  }
}

task sourceJar(type: Jar) {
  classifier = "sources"
  from sourceSets.main.allSource
}

publishing {
  publications {
    plugin(MavenPublication) {
      groupId project.group
      artifactId project.archivesBaseName
      version project.version

      from components['java']

      artifact sourceJar
    }
  }
  repositories {
    maven {
      url "http://mavenupload.modmuss50.me/"
      if (project.hasProperty('mavenPass')) {
        credentials {
          username 'buildslave'
          password project.getProperty('mavenPass')
        }
      }
    }
  }
}
