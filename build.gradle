apply plugin: 'java'

compileJava {
  sourceCompatibility '1.8'
  targetCompatibility '1.8'
}

group = 'org.jetbrains'
archivesBaseName = 'intellij-fernflower'

version = '1.1.0'

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = version + "." + "${ENV.BUILD_NUMBER}"
}

sourceSets {
  main.java.srcDirs 'src'
  test.java.srcDirs 'test'
}

repositories { jcenter() }
dependencies {
  testCompile 'junit:junit:4.+'
  testCompile 'org.assertj:assertj-core:3.+'
}

jar {
  manifest {
    attributes 'Main-Class': 'org.jetbrains.java.decompiler.main.decompiler.ConsoleDecompiler'
  }
}

task sourceJar(type:Jar) {
  classifier = "sources"
  from sourceSets.main.allSource
}

artifacts {
  archives sourceJar
}

apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/maven.gradle'
